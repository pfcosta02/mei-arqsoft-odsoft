@Library('jenkins') _
import org.jenkinsPipeline.Constants

properties([
    parameters([
        choice(
            name: "RolloutStrategy",
            choices: ['Blue/Green', 'Canary', 'Rolling'],
            description: 'Choose a Rollout Strategy.'
        ),
        string(
            name: "CanaryPercentage",
            defaultValue: '10',
            description: 'Percentage of traffic for canary (1-100). Only used for Canary strategy.'
        )
    ])
])

node {
    def MAVEN_DIR = tool(name: 'Maven 3.9.11', type: 'maven')
    def PROJECT_VERSION = ''
    def IMAGE_NAME = 'lendings-q'
    def RESULT_STR = ''
    def CURRENT_COLOR
    def NEW_COLOR
    def NAMESPACE = 'dev'
    def DOCKER_IMAGE = ''
    def CANARY_PERCENTAGE = params.CanaryPercentage.toInteger()

    try {
        stage('Checkout') {
            checkout scm
            if (isUnix()) {
                sh 'git clean -fdx'
            } else {
                bat 'git clean -fdx'
            }
        }

        stage('Set Maven Path') {
            if (isUnix()) {
                env.PATH = "${MAVEN_DIR}/bin:${env.PATH}"
            } else {
                env.PATH = "${MAVEN_DIR}\\bin;${env.PATH}"
            }
        }

        dir(IMAGE_NAME) {
            stage('Clean + Build') {
                CleanAndBuild()
            }

            stage('Unit Tests') {
                UnitTests()
            }

            stage('Integration Tests') {
                IntegrationTests()
            }

            stage('Consumer Contract Tests (Pact)') {
                ConsumerContractTests(true)
            }

            stage('SonarQube Static Code Analysis') {
                def sonarServer = Constants.ENVIRONMENT_2_SONARQUBE_SERVER[params.Environment]
                echo "Running SonarQube analysis using server: ${sonarServer}"
            }

            stage('Package') {
                PROJECT_VERSION = Package()
            }

            stage('Build Docker Image') {
                RESULT_STR = "${IMAGE_NAME}:${PROJECT_VERSION}"
                DOCKER_IMAGE = "${Constants.DOCKER_REGISTRY_D}/${RESULT_STR}"
                BuildDockerImage(RESULT_STR)
            }

            stage('Push Docker Image') {
                PushDockerImageD(RESULT_STR)
            }

            stage('Deploy') {
                EnsureK8sStructure(IMAGE_NAME, NAMESPACE)

                if (params.RolloutStrategy == 'Blue/Green') {
                    CURRENT_COLOR = GetCurrentDeploymentColor(IMAGE_NAME, NAMESPACE)
                    NEW_COLOR = (CURRENT_COLOR == 'blue') ? 'green' : 'blue'
                    DeployNewColor(IMAGE_NAME, NEW_COLOR, DOCKER_IMAGE, NAMESPACE)
                    WaitForDeploymentReady(IMAGE_NAME, NEW_COLOR, NAMESPACE)
                } else if (params.RolloutStrategy == 'Canary') {
                    echo "Deploying using CANARY strategy (${CANARY_PERCENTAGE}% initial traffic)..."
                    DeployCanaryInstance(IMAGE_NAME, DOCKER_IMAGE, NAMESPACE, CANARY_PERCENTAGE)
                } else {
                    echo "Deployment strategy ${params.RolloutStrategy} is not implemented yet."
                }
            }

            stage('Test Instance') {
                if (params.RolloutStrategy == 'Blue/Green') {
                    echo "Running tests on ${NEW_COLOR} instance..."
                } else if (params.RolloutStrategy == 'Canary') {
                    echo "Running smoke tests on CANARY instance..."
                    ValidateCanaryDeployment(IMAGE_NAME, NAMESPACE)
                    echo "Canary smoke tests PASSED - Ready for manual validation"
                }
            }

            stage('Manual Approval') {
                if (params.RolloutStrategy == 'Canary') {
                    try {
                        timeout(time: 30, unit: 'MINUTES') {
                            input message: "Canary looks good? Promote ${IMAGE_NAME}:${PROJECT_VERSION} to 100% traffic?",
                                  ok: 'Promote',
                                  submitter: 'admin,authenticated'
                        }
                        echo "Manual approval received. Promoting canary..."
                    } catch (err) {
                        echo "Canary REJECTED or TIMEOUT. Rolling back..."
                        RollbackCanary(IMAGE_NAME, NAMESPACE)
                        currentBuild.result = 'ABORTED'
                        throw new Exception("Canary validation denied or timed out")
                    }
                }
            }

            stage('Switch Traffic to New Instance') {
                if (params.RolloutStrategy == 'Blue/Green') {
                    SwitchTraffic(IMAGE_NAME, NEW_COLOR, NAMESPACE)
                } else if (params.RolloutStrategy == 'Canary') {
                    PromoteCanaryToProduction(IMAGE_NAME, NAMESPACE)
                }
            }

            stage('Undeploy old service') {
                if (params.RolloutStrategy == 'Blue/Green') {
                    if (CURRENT_COLOR != 'none') {
                        UndeployOldColor(IMAGE_NAME, CURRENT_COLOR, NAMESPACE)
                    }
                } else if (params.RolloutStrategy == 'Canary') {
                    // j√° removemos o canary em PromoteCanaryToProduction / RollbackCanary
                    echo "No extra undeploy step needed for Canary."
                }
            }
        }

        echo 'Pipeline completed successfully.'
        if (isUnix()) {
            archiveArtifacts artifacts: "**/target/pit-reports/index.html,**/target/pacts/*.json", allowEmptyArchive: true
            junit skipPublishingChecks: true, testResults: '**/target/surefire-reports/*.xml'
        } else {
            archiveArtifacts artifacts: "**\\target\\pit-reports\\index.html,**\\target\\pacts\\*.json", allowEmptyArchive: true
            junit skipPublishingChecks: true, testResults: '**\\target\\surefire-reports\\*.xml'
        }

    } catch (Exception e) {
        echo "ERROR FOUND! ${e.getMessage()}"
        currentBuild.result = 'FAILURE'
        throw e
    } finally {
        cleanWs(
            patterns: [[pattern: '**/*', type: 'INCLUDE']],
            deleteDirs: true,
            notFailBuild: true
        )
    }
}
