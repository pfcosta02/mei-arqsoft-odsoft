@Library('jenkins')
import org.jenkinsPipeline.Constants

// Scripted Pipeline for Lendings Q Service
properties([
    parameters([
        choice(
            name: "RolloutStrategy",
            choices: ['Blue/Green', 'Canary', 'Rolling'],
            description: 'Choose a Rollout Strategy.'
        ),
        string(
            name: "CanaryPercentage",
            defaultValue: '10',
            description: 'Percentage of traffic for canary (1-100). Only used for Canary strategy.'
        )
    ])
])

node {
    def MAVEN_DIR = tool(name: 'Maven 3.9.11', type: 'maven')
    def PROJECT_VERSION = ''
    def IMAGE_NAME = 'lendings-q'
    def RESULT_STR = ''
    def CURRENT_COLOR // Cor atual do deployment (blue, green ou none) - SÓ para Blue/Green
    def NEW_COLOR // Nova cor para o deployment - SÓ para Blue/Green
    def NAMESPACE = 'dev' // Kubernetes namespace
    def DOCKER_IMAGE = ''
    def CANARY_PERCENTAGE = params.CanaryPercentage.toInteger()

    try
    {
        stage('Checkout')
        {
            echo 'Checking out source code...'
            checkout scm

            // Clean untracked files and directories from Git workspace
            echo 'Cleaning untracked files from Git...'
            if (isUnix())
            {
                sh 'git clean -fdx'
            }
            else
            {
                bat 'git clean -fdx'
            }
        }

        stage('Set Maven Path')
        {
            if (isUnix())
            {
                env.PATH = "${MAVEN_DIR}/bin:${env.PATH}"
            }
            else
            {
                env.PATH = "${MAVEN_DIR}\\bin;${env.PATH}"
            }
            echo "Maven added to PATH: ${env.PATH}"
        }

        dir(IMAGE_NAME)
        {
            stage('Clean + Build')
            {
                CleanAndBuild();
            }

            stage('Unit Tests')
            {
                UnitTests();
            }

            stage('Integration Tests')
            {
                IntegrationTests();
            }

            stage('Consumer Contract Tests (Pact)')
            {
                // true significa que é um consumer
                // ConsumerContractTests(true);
            }

            stage('SonarQube Static Code Analysis')
            {
                def sonarServer = Constants.ENVIRONMENT_2_SONARQUBE_SERVER[params.Environment]
                echo "Running SonarQube analysis using server: ${sonarServer}"
            }

            stage('SonarQube Quality Gate')
            {
                // timeout(time: 5, unit: 'MINUTES') {
                //     def qualityGateResult = waitForQualityGate(abortPipeline: true)
                //     if (qualityGateResult.status == 'OK') {
                //         echo 'Quality gate passed. Proceeding with the pipeline.'
                //     } else {
                //         error "Quality gate failed: ${qualityGateResult.status}"
                //     }
                // }
            }

            stage('Package')
            {
                PROJECT_VERSION = Package();
            }

            stage('Build Docker Image')
            {
                RESULT_STR = "${IMAGE_NAME}:${PROJECT_VERSION}"
                DOCKER_IMAGE = "${Constants.DOCKER_REGISTRY_D}/${RESULT_STR}"
                BuildDockerImage(RESULT_STR);
            }

            stage('Push Docker Image')
            {
                PushDockerImageD(RESULT_STR);
            }

            stage('Deploy')
            {
                /*
                * Garante infraestrutura base do Kubernetes
                * (namespace, service, RabbitMQ) - comum a todas as estratégias
                */
                EnsureK8sStructure(IMAGE_NAME, NAMESPACE)

                if (params.RolloutStrategy == 'Blue/Green')
                {
                    echo 'Deploying the application using Blue/Green strategy...'
                    CURRENT_COLOR = GetCurrentDeploymentColor(IMAGE_NAME, NAMESPACE)
                    echo "Current active deployment color: ${CURRENT_COLOR}"
                    NEW_COLOR = (CURRENT_COLOR == 'blue') ? 'green' : 'blue'
                    echo "New deployment color will be: ${NEW_COLOR}"
                    DeployNewColor(IMAGE_NAME, NEW_COLOR, DOCKER_IMAGE, NAMESPACE)
                    WaitForDeploymentReady(IMAGE_NAME, NEW_COLOR, NAMESPACE)
                }
                else if (params.RolloutStrategy == 'Canary')
                {
                    echo 'Deploying the application using Canary strategy...'

                    /*
                    * CANARY DEPLOYMENT - STAGED ROLLOUT (SEM BLUE/GREEN)
                    *
                    * Estratégia de deploy com validação gradual:
                    * 1. Deploy nova versão canary isolada (1 replica)
                    * 2. Deployment stable continua servindo 100% tráfego (2 replicas)
                    * 3. Smoke tests na instância canary (0% tráfego até aprovação)
                    * 4. Se tudo OK, promove canary para versão estável
                    * 5. Remove deployment canary
                    *
                    * VANTAGENS:
                    * - Sem lógica de cores (simples e direto)
                    * - Canary isolado até aprovação
                    * - Rollback fácil (apenas remove canary)
                    * - Dados reais para validar performance
                    */

                    // Cria deployment canary paralelo (isolado)
                    DeployCanaryInstance(IMAGE_NAME, DOCKER_IMAGE, NAMESPACE, CANARY_PERCENTAGE)
                    WaitForDeploymentReady(IMAGE_NAME, 'canary', NAMESPACE)
                }
                else if (params.RolloutStrategy == 'Rolling')
                {
                    echo "Deployment strategy ${params.RolloutStrategy} is not implemented yet."
                }
                else
                {
                    echo "Unknown deployment strategy: ${params.RolloutStrategy}"
                    error "Invalid rollout strategy"
                }
            }

            stage('Test Instance')
            {
                if (params.RolloutStrategy == 'Blue/Green')
                {
                    echo "Running tests on ${NEW_COLOR} instance..."
                    echo "All tests passed on ${NEW_COLOR} instance - SAFE TO SWITCH"
                }
                else if (params.RolloutStrategy == 'Canary')
                {
                    echo "Running smoke tests on canary instance (${CANARY_PERCENTAGE}% traffic, teórico)..."

                    /*
                    * Validação da versão canary
                    * - Health checks básicos
                    * - Endpoints críticos
                    * - Sem validação completa (apenas smoke tests)
                    */
                    ValidateCanaryDeployment(IMAGE_NAME, NAMESPACE)

                    echo "Canary smoke tests PASSED - Ready for traffic validation"
                }
                else if (params.RolloutStrategy == 'Rolling')
                {
                    echo "Testing strategy for ${params.RolloutStrategy} is not implemented yet."
                }
            }

            stage('Manual Approval')
            {
                if (params.RolloutStrategy == 'Canary')
                {
                    echo """
                        ==========================================
                        CANARY VALIDATION APPROVAL REQUIRED
                        ==========================================
                        Service: ${IMAGE_NAME}
                        New Version: ${PROJECT_VERSION}
                        Canary Traffic: ${CANARY_PERCENTAGE}% (teórico)
                        Current Stable: Still running previous version
                        Namespace: ${NAMESPACE}
                        Docker Image: ${DOCKER_IMAGE}
                        ==========================================
                        Canary smoke tests have PASSED.
                        Ready to promote canary to 100% production traffic?
                        ==========================================
                    """

                    try
                    {
                        timeout(time: 30, unit: 'MINUTES')
                        {
                            input message: "Promote canary version ${PROJECT_VERSION} to 100% production traffic?",
                                  ok: 'Promote to Production',
                                  submitter: 'admin,authenticated'
                        }
                        echo "Canary APPROVED by user. Proceeding with promotion..."
                    }
                    catch (err)
                    {
                        echo "Canary validation REJECTED or TIMEOUT. Performing rollback..."
                        RollbackCanary(IMAGE_NAME, NAMESPACE)
                        currentBuild.result = 'ABORTED'
                        throw new Exception("Canary promotion rejected or timed out")
                    }
                }
                else if (params.RolloutStrategy == 'Blue/Green')
                {
                    echo """
                        ==========================================
                        DEPLOYMENT APPROVAL REQUIRED
                        ==========================================
                        Service: ${IMAGE_NAME}
                        New Version: ${PROJECT_VERSION}
                        Target Color: ${NEW_COLOR}
                        Current Color: ${CURRENT_COLOR}
                        Namespace: ${NAMESPACE}
                        Docker Image: ${DOCKER_IMAGE}
                        ==========================================
                        Tests have PASSED. Waiting for manual approval to switch traffic...
                        ==========================================
                    """

                    try
                    {
                        timeout(time: 30, unit: 'MINUTES')
                        {
                            input message: "Deploy ${IMAGE_NAME}:${PROJECT_VERSION} to production?",
                                  ok: 'Deploy',
                                  submitter: 'admin,authenticated'
                        }
                        echo "Deployment APPROVED by user. Proceeding with traffic switch..."
                    }
                    catch (err)
                    {
                        echo "Deployment REJECTED or TIMEOUT. Aborting deployment..."
                        RollbackBlueGreen(IMAGE_NAME, NEW_COLOR, CURRENT_COLOR, NAMESPACE)
                        currentBuild.result = 'ABORTED'
                        throw new Exception("Manual approval denied or timed out")
                    }
                }
            }

            stage('Switch Traffic to New Instance')
            {
                if (params.RolloutStrategy == 'Blue/Green')
                {
                    echo "Switching traffic from ${CURRENT_COLOR} to ${NEW_COLOR}..."
                    SwitchTraffic(IMAGE_NAME, NEW_COLOR, NAMESPACE)
                    echo "Traffic successfully switched to ${NEW_COLOR}"
                }
                else if (params.RolloutStrategy == 'Canary')
                {
                    echo "Promoting canary to 100% traffic..."
                    PromoteCanaryToProduction(IMAGE_NAME, NAMESPACE)
                    echo "Canary successfully promoted to 100% traffic"
                }
            }

            stage('Undeploy old service')
            {
                if (params.RolloutStrategy == 'Blue/Green')
                {
                    echo "Removing old ${CURRENT_COLOR} deployment..."
                    if (CURRENT_COLOR != 'none')
                    {
                        UndeployOldColor(IMAGE_NAME, CURRENT_COLOR, NAMESPACE)
                        echo "${CURRENT_COLOR} deployment removed successfully"
                    }
                    else
                    {
                        echo "No old deployment to remove (first deployment)"
                    }
                }
                else if (params.RolloutStrategy == 'Canary')
                {
                    echo "Canary deployment already removed during promotion"
                    echo "Pipeline completed successfully"
                }
           }
        }

        // Success actions
        echo 'Pipeline completed successfully.'
        if (isUnix()) {
            archiveArtifacts artifacts: "**/target/pit-reports/index.html,**/target/pacts/*.json", allowEmptyArchive: true
            junit skipPublishingChecks: true, testResults: '**/target/surefire-reports/*.xml'
        } else {
            archiveArtifacts artifacts: "**\\target\\pit-reports\\index.html,**\\target\\pacts\\*.json", allowEmptyArchive: true
            junit skipPublishingChecks: true, testResults: '**\\target\\surefire-reports\\*.xml'
        }

    } catch (Exception e) {
        echo "ERROR FOUND! ${e.getMessage()}"
        currentBuild.result = 'FAILURE'
        throw e
    } finally {
        echo 'Performing cleanup...'
        cleanWs(
            patterns: [
                [pattern: '**/*', type: 'INCLUDE']
            ],
            deleteDirs: true,
            notFailBuild: true
        )
    }
}